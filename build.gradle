buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath "edu.wpi.first:GradleRIO:2019.1.1"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.15.0"
    }
}

configure(subprojects) {
    group = 'org.strykeforce.thirdcoast'
    version = '19.2.0'

    apply plugin: 'java-library'
    apply plugin: 'idea'

    repositories {
        jcenter()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'

        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

        // Testing
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.+"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.+"
        testImplementation "org.assertj:assertj-core:3.+"
        testImplementation "org.mockito:mockito-junit-jupiter:2.+"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:5.+"
        testRuntime 'ch.qos.logback:logback-classic:1.2.3'
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
            sourceDirs += files('build/generated/source/kaptKotlin/main')
            generatedSourceDirs += files('build/generated/source/kaptKotlin/main')
        }
    }

    test {
        useJUnitPlatform()
    }
}

configure(subprojects - project(":deadeye")) {
    apply plugin: "edu.wpi.first.GradleRIO"
    
    dependencies {
        compile wpi.deps.wpilib()
        compile wpi.deps.vendor.java()
    }
}
