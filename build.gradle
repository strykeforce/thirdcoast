plugins {
    id "java"
    id "idea"
    id "net.ltgt.apt-idea" version "0.14"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.22" apply false
    id "net.ltgt.errorprone" version "0.0.13"
    id "com.diffplug.gradle.spotless" version "3.9.0"
}

description = 'Third Coast Framework'

ext {
    libs = [
            jetbrainsAnnotations: 'org.jetbrains:annotations:15.0',
            jsr305              : 'com.google.code.findbugs:jsr305:3.0.2',
            bytebuddy           : 'net.bytebuddy:byte-buddy:1.7.10',
            dagger              : 'com.google.dagger:dagger:2.14.1',
            daggerCompiler      : 'com.google.dagger:dagger-compiler:2.14.1',
            logback             : 'ch.qos.logback:logback-classic:1.2.3',
            moshi               : 'com.squareup.moshi:moshi:1.5.0',
            nanohttpd           : 'org.nanohttpd:nanohttpd:2.3.2-SNAPSHOT',
            objenesis           : 'org.objenesis:objenesis:2.6',
            slf4j               : 'org.slf4j:slf4j-api:1.7.25',
            spock               : 'org.spockframework:spock-core:1.1-groovy-2.4',
            toml4j              : 'com.moandjiezana.toml:toml4j:0.7.2',
            autoFactory         : 'com.google.auto.factory:auto-factory:1.0-beta5',
    ]
    specificationTitle = 'Third Coast Library'
    specificationVersion = '18.3.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jaci.openrio.gradle.GradleRIO'
    apply plugin: 'net.ltgt.apt-idea'
    apply plugin: 'net.ltgt.errorprone'

    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // nanohttpd
    }
}

spotless {
    java {
        googleJavaFormat()
        target '**/*.java'
    }
}

idea {
    module {
        apt {
            addGeneratedSourcesDirs = true
            addAptDependencies = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
