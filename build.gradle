buildscript {
  ext {
    kotlinVersion = '1.3.60'
    moshiVersion = '1.8.0'
    jettyVersion = '9.4.19.v20190610'
    okhttpVersion = '3.12.5'
    slf4jVersion = '1.7.28'
    logbackVersion = '1.2.3'
    kotlinLoggingVersion = '1.7.6'
    dokkaVersion = '0.9.18'
    junitVersion = '5.+'
    jsonAssert = '1.+'
    assertJVersion = '3.+'
    mockitoVersion = '2.+'
    gradleRioVersion = '2019.4.1'
    bintrayVersion = '1.+'
    spotlessVersion = '3.+'
    wpiVersion = '2019.4.1'
  }
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
    classpath "edu.wpi.first:GradleRIO:$gradleRioVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
  }
}

// applies to all sub-projects
configure(subprojects) {
  group = 'org.strykeforce.thirdcoast'
  version = '19.5.0'

  apply plugin: 'java-library'
  apply plugin: 'idea'

  sourceCompatibility = 11

  repositories {
    jcenter()
  }

  dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
  }

  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }

  test {
    useJUnitPlatform()
  }
}

// applies to all sub-projects except for deadeye
configure(subprojects - project(":deadeye")) {
  apply plugin: "edu.wpi.first.GradleRIO"

  dependencies {
    implementation wpi.deps.wpilib()
    implementation wpi.deps.vendor.java()
  }
}

// applies to all Kotlin sub-projects
configure(subprojects - project(":swerve")) {
  apply plugin: "kotlin"
  apply plugin: "kotlin-kapt"
  apply plugin: 'org.jetbrains.dokka'

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
  }
}
